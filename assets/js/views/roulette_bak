class GiddyApps.Views.RouletteBallView extends Backbone.View


  initialize: (options = {}) ->
    @stage = options.stage
    @on "spin-start", @spin_start
    @on "spin-stop", @spin_stop
    @render()

  leave: ->
    @remove()

  render: ->
    @_setup_ball()
    this

  spin_start: =>
    @_randomize_to_number()
    @_reset_ball()
    @_animate_spin()

  _reset_ball: ->
    return unless @tween
    @tween.reset()
    @ball_el.setAttrs(rotation: 0, offsetX: 155)

  _setup_ball: ->
    # NOTE: Make this configurable
    @ball_el = new Kinetic.Circle(
      x:  @stage.getWidth() / 2
      y: @stage.getHeight() / 2
      radius: 5
      fill: "#ffffff"
      stroke: "#bbbbbb"
      strokeWidth: 1
      offset: [155,0]
    )

    # NOTE: consider augmenting el to return canvas el instead of dom el?
    # and moving the stage manipulation up on the parent view.
    @ball_layer = new Kinetic.Layer()
    @ball_layer.add(@ball_el)
    @stage.add(@ball_layer)

  velocity: 4 * Math.PI

  _animate_spin: ->
    rando = _.random(2,4)
    @tween = new Kinetic.Tween(
      node: @ball_el
      rotation: @velocity * rando
      offsetX: 150
      duration: rando
      easing: Kinetic.Easings.Linear
      onFinish: => @_animate_descent()
    )
    @tween.play()

  _animate_descent: ->
    rotation_amt = @ball_el.getRotation() + @_radians_for(@selected_num) + @velocity
    @tween = new Kinetic.Tween(
      node: @ball_el
      rotation: rotation_amt
      duration: 1 + @_radians_for(@selected_num) / @velocity
      offsetX: 96
      easing: Kinetic.Easings.Linear
      onFinish: => @_animate_selection()
    )
    @tween.play()

  _animate_selection: ->
    rotation_amt = @ball_el.getRotation() + @velocity
    @tween = new Kinetic.Tween(
      node: @ball_el
      rotation: rotation_amt
      duration: 3
      offsetX: 76
      easing: Kinetic.Easings.EaseOut
      onFinish: =>
        console.log "Selected #{@selected_num}"
    )
    @tween.play()

  # MOVE this to MODEL
  _randomize_to_number: ->
    @selected_num = _(@DOUBLE_ZERO_WHEEL).sample()
    # @radians_at_stop = (@random_spins * 2 * Math.PI) + @_radians_for(@selected_num)

  _radians_for: (str_num) ->
    num_pos = _(@DOUBLE_ZERO_WHEEL).indexOf(str_num)
    @RADIANS_PER_NUM * num_pos
